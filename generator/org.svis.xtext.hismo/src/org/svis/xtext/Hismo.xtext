grammar org.svis.xtext.Hismo  with org.svis.xtext.Famix


import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.svis.org/famix" as famix

generate hismo "http://www.svis.org/hismo"


HismoRoot: 
	hismoDocument=HismoDocument?;
	
HismoDocument:
	{HismoDocument}
	'(' elements+=(HISMOElement|FAMIXElement)*	')';


HISMOElement returns famix::FAMIXElement:
	HISMONamespaceHistory | 
	HISMOClassHistory | 
	HISMOMethodHistory | 
	HISMOAttributeHistory  | 
	HISMONamespaceVersion | 
	HISMOClassVersion | 
	HISMOMethodVersion | 
	HISMOAttributeVersion |
	HISMOIssue
;


//history elements
HISMONamespaceHistory:
    '(HISMO.NamespaceHistory'
	'(id:' name=INT_ID ')'
	('(name:' value= MSESTRING ')')?
	('(containingNamespaceHistory' containingNamespaceHistory=IntegerReference ')')? //parent
	('(namespaceHistories' namespaceHistories+=IntegerReference* ')')?
	('(namespaceVersions' namespaceVersions+=IntegerReference* ')')?
	('(classHistories' classHistories+=IntegerReference* ')')?
	')'
;

HISMOClassHistory:
	'(HISMO.ClassHistory' 
	'(id:' name=INT_ID ')'
	('(name:' value= MSESTRING ')')?
	('(classVersions' classVersions+=IntegerReference* ')')?
	('(containingNamespaceHistory' containingNamespaceHistory=IntegerReference ')')? //parentelement can be class or namespace TODO change
	('(methodHistories' methodHistories+=IntegerReference* ')')?
	('(attributeHistories' attributeHistories+=IntegerReference* ')')?
	('(evolutionNumberOfStatements' evolutionNumberOfStatements=INT ')' )? 
	('(lastAuthor' lastAuthor=MSESTRING ')')?
	('(avgNumberOfOpenIncidents' avgNumberOfOpenIncidents = INT ')')?
	('(avgNumberOfClosedIncidents' avgNumberOfClosedIncidents = INT ')')?
	('(numberOfOpenSecurityIncidents' numberOfOpenSecurityIncidents = INT ')')?
	('(numberOfClosedSecurityIncidents' numberOfClosedSecurityIncidents = INT ')')?
	('(issues' issues += MSESTRING* ')')?
	')';	
	
HISMOMethodHistory:
	'(HISMO.MethodHistory' 
	'(id:' name=INT_ID ')'
	('(name:' value= MSESTRING ')')?
	('(methodVersions' methodVersions+=IntegerReference* ')')?
	('(containingClassHistory' containingClassHistory=IntegerReference ')')?
	('(signature' signature=MSESTRING ')')?
	//Measurements
	('(maxNumberOfStatements' maxNumberOfStatements=INT ')' )?
	('(minNumberOfStatements' minNumberOfStatements=INT ')' )?
	('(averageNumberOfStatements' averageNumberOfStatements=INT ')' )?
	('(evolutionNumberOfStatements' evolutionNumberOfStatements=INT ')' )? 
	')'
;

HISMOAttributeHistory:
	'(HISMO.AttributeHistory'
	'(id:' name=INT_ID ')' 
	('(name:' value= MSESTRING ')')?
	('(attributeVersions' attributeVersions+=IntegerReference* ')')?
	('(containingClassHistory' containingClassHistory=IntegerReference ')')?
	')'
;

//version elements
HISMONamespaceVersion:
	'(HISMO.NamespaceVersion'
	'(id:' name=INT_ID ')'
	'(name' value=MSESTRING ')'
	('(' 'container' container=IntegerReference ')')?
	('(' 'hash' id = STRING ')')?
	('(parentHistory' parentHistory=IntegerReference ')')?	
	('(classVersions' classVersions+=IntegerReference* ')')?
	('(versionEntity' versionEntity=IntegerReference ')')?
	('(author' author=MSESTRING ')')?	
	('(timestamp' timestamp=MSESTRING ')')?
	('(commitId' commitId=MSESTRING ')')?	
	('(isProject' isProject = Boolean ')')?
	')';
	
HISMOClassVersion:
	'(HISMO.ClassVersion'
	'(id:' name=INT_ID ')'
	'(name' value=MSESTRING ')'
	('(' 'hash' id = STRING ')')?
	('(container' container=IntegerReference ')')?
	('(parentHistory' parentHistory=IntegerReference ')')?	
	('(methodVersions' methodVersions+=IntegerReference* ')')?
	('(attributeVersions' attributeVersions+=IntegerReference* ')')?
	('(versionEntity' versionEntity=IntegerReference ')')?
	('(author' author=MSESTRING ')')?	
	('(timestamp' timestamp=MSESTRING')')?
	('(scc' scc=IntegerReference ')')?
	('(antipattern' antipattern +=IntegerReference* ')')?
	('(commitId' commitId=MSESTRING ')')?	
	('(betweennessCentrality' betweennessCentrality = MSESTRING ')')?
	('(' 'STK-Rank' stkRank = MSESTRING ')')?
	('(evolutionNumberOfStatements' evolutionNumberOfStatements=INT ')' )? //Sum of absolute Changes in subsequent Versions
	')';

HISMOMethodVersion:
	'(HISMO.MethodVersion'
	'(id:' name=INT_ID ')'
	'(name' value=MSESTRING ')'
	('(' 'hash' id = STRING ')')?
	('(' 'container' container=IntegerReference ')')?
	('(parentHistory' parentHistory=IntegerReference ')')?	
	('(versionEntity' versionEntity=IntegerReference ')')?
	('(author' author=MSESTRING ')')?	
	('(timestamp' timestamp=Timestamp')')?
	('(commitId' commitId=MSESTRING ')')?	
	//Measurements	
	('(evolutionNumberOfStatements' evolutionNumberOfStatements=INT ')' )? //Sum of absolute Changes in subsequent Versions
	')';

HISMOAttributeVersion:
	'(HISMO.AttributeVersion'
	'(id:' name=INT_ID ')'
	'(name' value=MSESTRING ')'
	('(' 'hash' id = STRING ')')?
	('(' 'container' container=IntegerReference ')')?
	('(parentHistory' parentHistory=IntegerReference ')')?	
	('(versionEntity' versionEntity=IntegerReference ')')?
	('(author' author=MSESTRING ')')?	
	('(timestamp' timestamp=Timestamp')')?
	('(commitId' commitId=MSESTRING ')')?	
	')';
	
HISMOIssue:
	'(HISMO.Issues'
	'(id:' name=INT_ID ')'
	'(name' value=MSESTRING ')'
	'(' 'open' open = Boolean ')'
	'(' 'security' security = Boolean ')'
	')';
	
Timestamp returns ecore::EString : MSESTRING;
